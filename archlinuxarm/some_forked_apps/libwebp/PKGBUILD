pkgname=libwebp
pkgver=1.2.1
pkgrel=2
pkgdesc="WebP library and conversion tools"
arch=(armv7h)
url="https://developers.google.com/speed/webp/"
license=(BSD)
depends=(libpng libjpeg libtiff giflib)
makedepends=(freeglut mesa glu)
optdepends=('freeglut: vwebp viewer')
provides=(libwebp{,decoder,demux,mux}.so)
source=(https://storage.googleapis.com/downloads.webmproject.org/releases/webp/$pkgname-$pkgver.tar.gz)
sha256sums=('SKIP')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D') # WebP release signing key

build() {
  cd $pkgname-$pkgver
 # [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"
  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-swap-16bit-csp \
    --enable-libwebp{mux,demux,decoder,extras} $CONFIG
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
